// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import { IVoterIdNFT } from "../interfaces/IVoterIdNFT.sol";
import { ContentRegistry } from "../ContentRegistry.sol";
import { RoundVotingEngine } from "../RoundVotingEngine.sol";
import { RoundLib } from "./RoundLib.sol";

library QuestionRewardPoolEscrowQualificationLib {
    error RewardPoolCursorNeedsAdvance();

    struct QualificationContext {
        RoundVotingEngine votingEngine;
        IVoterIdNFT voterIdNft;
        uint256 contentId;
        uint256 roundId;
        uint64 bountyClosesAt;
        uint32 requiredVoters;
        address funder;
        address funderIdentity;
        uint256 funderNullifier;
        address submitterIdentity;
        uint256 submitterNullifier;
    }

    function previewRoundQualification(QualificationContext memory ctx)
        external
        view
        returns (bool roundSettled, bool canQualify, uint256 eligibleVoters, uint48 settledAt)
    {
        (, RoundLib.RoundState state,,,,,,,,, uint48 roundSettledAt,,,) =
            ctx.votingEngine.rounds(ctx.contentId, ctx.roundId);
        if (state != RoundLib.RoundState.Settled || roundSettledAt == 0) return (false, false, 0, 0);
        settledAt = roundSettledAt;

        roundSettled = true;
        eligibleVoters = _countEligibleRevealedVoters(ctx);
        canQualify = eligibleVoters >= ctx.requiredVoters;
    }

    function requireNoPendingFinishedRound(
        RoundVotingEngine votingEngine,
        uint256 contentId,
        uint256 nextRoundToEvaluate,
        uint64 bountyClosesAt
    ) external view {
        (uint48 startedAt, RoundLib.RoundState state,,,,,,,,,,,,) = votingEngine.rounds(contentId, nextRoundToEvaluate);
        if (state == RoundLib.RoundState.Open) {
            if (startedAt == 0 || (bountyClosesAt != 0 && startedAt > bountyClosesAt)) return;
        }
        revert RewardPoolCursorNeedsAdvance();
    }

    function isExcludedVoter(
        IVoterIdNFT voterIdNft,
        uint256 voterId,
        address funder,
        address funderIdentity,
        uint256 funderNullifier,
        address submitterIdentity,
        uint256 submitterNullifier
    ) external view returns (bool) {
        return _isExcludedVoter(
            voterIdNft, voterId, funder, funderIdentity, funderNullifier, submitterIdentity, submitterNullifier
        );
    }

    function resolveSubmitterNullifier(ContentRegistry registry, IVoterIdNFT voterIdNft, uint256 contentId)
        external
        view
        returns (uint256)
    {
        uint256 snapshottedNullifier = registry.contentSubmitterNullifier(contentId);
        if (snapshottedNullifier != 0) return snapshottedNullifier;

        address account = registry.getSubmitterIdentity(contentId);
        uint256 voterId = voterIdNft.getTokenId(account);
        return voterId == 0 ? 0 : voterIdNft.getNullifier(voterId);
    }

    function isBundleExcludedVoter(
        IVoterIdNFT voterIdNft,
        address account,
        address funder,
        uint256 funderNullifier,
        uint256 submitterNullifier
    ) external view returns (bool) {
        uint256 voterId = voterIdNft.getTokenId(account);
        if (voterId == 0) return false;
        if (voterId == voterIdNft.getTokenId(funder)) return true;

        uint256 voterNullifier = voterIdNft.getNullifier(voterId);
        return voterNullifier != 0 && (voterNullifier == funderNullifier || voterNullifier == submitterNullifier);
    }

    function _countEligibleRevealedVoters(QualificationContext memory ctx)
        private
        view
        returns (uint256 eligibleVoters)
    {
        uint256 commitCount = ctx.votingEngine.getRoundCommitCount(ctx.contentId, ctx.roundId);
        for (uint256 i = 0; i < commitCount;) {
            bytes32 commitKey = ctx.votingEngine.getRoundCommitKey(ctx.contentId, ctx.roundId, i);
            (address voter,,, uint48 revealedAt, bool revealed,,) =
                ctx.votingEngine.commitCore(ctx.contentId, ctx.roundId, commitKey);
            if (voter != address(0) && revealed && _revealedByBountyClose(ctx.bountyClosesAt, revealedAt)) {
                uint256 voterId = ctx.votingEngine.commitVoterId(ctx.contentId, ctx.roundId, commitKey);
                if (!_isExcludedVoter(
                        ctx.voterIdNft,
                        voterId,
                        ctx.funder,
                        ctx.funderIdentity,
                        ctx.funderNullifier,
                        ctx.submitterIdentity,
                        ctx.submitterNullifier
                    )) {
                    unchecked {
                        ++eligibleVoters;
                    }
                }
            }
            unchecked {
                ++i;
            }
        }
    }

    function _revealedByBountyClose(uint64 bountyClosesAt, uint48 revealedAt) private pure returns (bool) {
        return bountyClosesAt == 0 || revealedAt <= bountyClosesAt;
    }

    function _isExcludedVoter(
        IVoterIdNFT voterIdNft,
        uint256 voterId,
        address funder,
        address funderIdentity,
        uint256 funderNullifier,
        address submitterIdentity,
        uint256 submitterNullifier
    ) private view returns (bool) {
        if (voterId == 0) return false;

        uint256 voterNullifier = voterIdNft.getNullifier(voterId);
        if (voterNullifier != 0 && (voterNullifier == funderNullifier || voterNullifier == submitterNullifier)) {
            return true;
        }

        if (
            voterId == _resolveFunderVoterId(voterIdNft, funder, funderIdentity)
                || voterId == voterIdNft.getTokenId(funder)
        ) {
            return true;
        }

        if (submitterIdentity != address(0) && voterId == voterIdNft.getTokenId(submitterIdentity)) {
            return true;
        }

        return false;
    }

    function _resolveFunderVoterId(IVoterIdNFT voterIdNft, address funder, address funderIdentity)
        private
        view
        returns (uint256)
    {
        if (funderIdentity != address(0)) {
            uint256 identityVoterId = voterIdNft.getTokenId(funderIdentity);
            if (identityVoterId != 0) return identityVoterId;
        }
        return voterIdNft.getTokenId(funder);
    }
}
