// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

import {ProtocolConfig} from "../ProtocolConfig.sol";
import {IVoterIdNFT} from "../interfaces/IVoterIdNFT.sol";
import {RoundLib} from "./RoundLib.sol";
import {TlockVoteLib} from "./TlockVoteLib.sol";
import {TokenTransferLib} from "./TokenTransferLib.sol";

interface IRoundCleanupActivityRegistry {
    function updateActivity(uint256 contentId) external;
}

/// @title RoundCleanupLib
/// @notice Shared refund and cleanup paths extracted from RoundVotingEngine to reduce runtime size.
library RoundCleanupLib {
    using SafeERC20 for IERC20;

    uint256 internal constant CLEANUP_INCENTIVE_BPS = 100; // 1%
    uint256 internal constant CLEANUP_INCENTIVE_MAX = 5e6; // 5 HREP

    error RoundNotCancelledOrTied();
    error AlreadyClaimed();
    error NoCommit();
    error NoStake();
    error VoteNotRevealed();
    error NothingProcessed();

    function targetRoundRevealableAt(
        mapping(uint256 => bytes32) storage roundDrandChainHashSnapshot,
        mapping(uint256 => uint64) storage roundDrandGenesisTimeSnapshot,
        mapping(uint256 => uint64) storage roundDrandPeriodSnapshot,
        ProtocolConfig protocolConfig,
        uint256 roundId,
        uint64 targetRound
    ) external view returns (uint256) {
        uint64 genesisTime = roundDrandGenesisTimeSnapshot[roundId];
        uint64 period = roundDrandPeriodSnapshot[roundId];
        if (roundDrandChainHashSnapshot[roundId] == bytes32(0) || genesisTime == 0 || period == 0) {
            genesisTime = protocolConfig.drandGenesisTime();
            period = protocolConfig.drandPeriod();
        }
        if (targetRound == 0 || genesisTime == 0 || period == 0) revert TlockVoteLib.TargetRoundOutOfWindow();
        return uint256(genesisTime) + (uint256(targetRound) - 1) * uint256(period);
    }

    function validateCommitTlockData(
        mapping(uint256 => bytes32) storage roundDrandChainHashSnapshot,
        mapping(uint256 => uint64) storage roundDrandGenesisTimeSnapshot,
        mapping(uint256 => uint64) storage roundDrandPeriodSnapshot,
        ProtocolConfig protocolConfig,
        uint256 roundId,
        bytes memory ciphertext,
        uint64 targetRound,
        bytes32 drandChainHash,
        uint256 epochEnd,
        uint256 epochDuration
    ) external view {
        bytes32 chainHash = roundDrandChainHashSnapshot[roundId];
        uint64 genesisTime = roundDrandGenesisTimeSnapshot[roundId];
        uint64 period = roundDrandPeriodSnapshot[roundId];

        if (chainHash == bytes32(0) || genesisTime == 0 || period == 0) {
            chainHash = protocolConfig.drandChainHash();
            genesisTime = protocolConfig.drandGenesisTime();
            period = protocolConfig.drandPeriod();
        }

        TlockVoteLib.validateCommitData(
            ciphertext, targetRound, drandChainHash, chainHash, epochEnd, epochDuration, genesisTime, period
        );
    }

    function resolveClaimCommit(
        mapping(address => bytes32) storage roundVoterCommitHash,
        mapping(uint256 => bytes32) storage roundVoterIdCommitKey,
        mapping(uint256 => bytes32) storage roundVoterNullifierCommitKey,
        mapping(bytes32 => uint256) storage,
        address voterIdNftAddress,
        address account
    ) external view returns (bytes32 commitKey, address rewardRecipient) {
        if (voterIdNftAddress != address(0)) {
            IVoterIdNFT voterIdNft = IVoterIdNFT(voterIdNftAddress);
            uint256 voterId = voterIdNft.getTokenId(account);
            if (voterId != 0) {
                commitKey = roundVoterIdCommitKey[voterId];
                if (commitKey == bytes32(0)) {
                    uint256 nullifier = voterIdNft.getNullifier(voterId);
                    if (nullifier != 0) {
                        commitKey = roundVoterNullifierCommitKey[nullifier];
                    }
                }

                rewardRecipient = voterIdNft.getHolder(voterId);
                if (rewardRecipient == address(0)) rewardRecipient = account;
                return (commitKey, rewardRecipient);
            }
        }

        bytes32 directCommitHash = roundVoterCommitHash[account];
        if (directCommitHash != bytes32(0)) {
            commitKey = keccak256(abi.encodePacked(account, directCommitHash));
            return (commitKey, account);
        }

        return (bytes32(0), account);
    }

    function recordCommitIndexes(
        bytes32[] storage roundCommitHashes,
        mapping(uint256 => uint256) storage epochUnrevealedCount,
        mapping(uint256 => uint256) storage lastCommitRevealableAfter,
        mapping(address => bytes32) storage roundVoterCommitHash,
        uint256 roundId,
        bytes32 commitKey,
        uint256 epochEnd,
        uint256 effectiveRevealableAfter,
        address voter,
        bytes32 commitHash
    ) external {
        roundCommitHashes.push(commitKey);
        epochUnrevealedCount[epochEnd]++;
        if (effectiveRevealableAfter > lastCommitRevealableAfter[roundId]) {
            lastCommitRevealableAfter[roundId] = effectiveRevealableAfter;
        }

        roundVoterCommitHash[voter] = commitHash;
    }

    function recordTokenIdentityCommitIndex(
        mapping(uint256 => bool) storage roundHasTokenIdCommitted,
        mapping(uint256 => bytes32) storage roundVoterIdCommitKey,
        mapping(bytes32 => uint256) storage roundCommitVoterId,
        mapping(uint256 => bytes32) storage roundVoterNullifierCommitKey,
        bytes32 commitKey,
        uint256 voterId,
        IVoterIdNFT voterIdNft
    ) external {
        roundHasTokenIdCommitted[voterId] = true;
        roundVoterIdCommitKey[voterId] = commitKey;
        roundCommitVoterId[commitKey] = voterId;
        uint256 voterNullifier = voterIdNft.getNullifier(voterId);
        if (voterNullifier != 0) {
            if (roundVoterNullifierCommitKey[voterNullifier] == bytes32(0)) {
                roundVoterNullifierCommitKey[voterNullifier] = commitKey;
            }
        }
    }

    function recordCommitAccounting(
        RoundLib.Round storage round,
        mapping(address => uint256) storage contentLastVoteTimestamp,
        mapping(uint256 => uint256) storage contentLastVoteTimestampByNullifier,
        address registry,
        IVoterIdNFT currentVoterIdNft,
        uint256 contentId,
        uint256 roundId,
        address voter,
        uint256 voterId,
        bool useTokenIdentity,
        uint64 stakeAmount64,
        uint256 stakeAmount
    ) external {
        round.voteCount++;
        round.totalStake += stakeAmount64;

        contentLastVoteTimestamp[voter] = block.timestamp;
        if (useTokenIdentity) {
            uint256 nullifier = currentVoterIdNft.getNullifier(voterId);
            if (nullifier != 0) contentLastVoteTimestampByNullifier[nullifier] = block.timestamp;
            currentVoterIdNft.recordStake(contentId, roundId, voterId, stakeAmount);
        }

        // Vote commits still refresh UI activity timestamps, but not the dormancy anchor.
        IRoundCleanupActivityRegistry(registry).updateActivity(contentId);
    }

    function claimCancelledRoundRefund(
        RoundLib.Round storage round,
        mapping(address => bool) storage refundClaims,
        mapping(bytes32 => bool) storage refundCommitClaims,
        mapping(bytes32 => RoundLib.Commit) storage roundCommits,
        IERC20 hrepToken,
        bytes32 commitKey
    ) external returns (uint256 refundAmount, address commitVoter) {
        if (
            round.state != RoundLib.RoundState.Cancelled && round.state != RoundLib.RoundState.Tied
                && round.state != RoundLib.RoundState.RevealFailed
        ) {
            revert RoundNotCancelledOrTied();
        }
        if (refundCommitClaims[commitKey]) revert AlreadyClaimed();

        RoundLib.Commit storage commit = roundCommits[commitKey];
        commitVoter = commit.voter;
        if (refundClaims[commitVoter]) revert AlreadyClaimed();
        refundAmount = commit.stakeAmount;
        if (refundAmount == 0) revert NoStake();
        if (round.state != RoundLib.RoundState.Cancelled && !commit.revealed) revert VoteNotRevealed();

        commit.stakeAmount = 0;
        refundCommitClaims[commitKey] = true;
        refundClaims[commitVoter] = true;

        hrepToken.safeTransfer(commitVoter, refundAmount);
    }

    function processUnrevealedVotes(
        RoundLib.Round storage round,
        bytes32[] storage commitKeys,
        mapping(bytes32 => RoundLib.Commit) storage roundCommits,
        mapping(uint256 => mapping(uint256 => uint256)) storage roundCleanupIncentivePaid,
        uint256 contentId,
        uint256 roundId,
        IERC20 hrepToken,
        ProtocolConfig protocolConfig,
        uint256 consensusReserve,
        address cleanupCaller,
        uint256 startIndex,
        uint256 count
    )
        external
        returns (
            uint256 forfeitedToTreasury,
            uint256 addedToConsensusReserve,
            uint256 refundedHrep,
            uint256 processedPastEpochCount,
            uint256 cleanupIncentive,
            uint256 updatedConsensusReserve
        )
    {
        uint256 len = commitKeys.length;
        // Treat `count == 0` and any `count` whose remaining range covers the rest of the
        // array as "process all". Use `unchecked` because callers may pass `type(uint256).max`
        // as a sentinel and `startIndex + count` would otherwise revert with Panic(0x11).
        uint256 endIndex;
        unchecked {
            uint256 candidate = startIndex + count;
            endIndex = (count == 0 || candidate < startIndex || candidate > len) ? len : candidate;
        }
        updatedConsensusReserve = consensusReserve;

        for (uint256 i = startIndex; i < endIndex; i++) {
            RoundLib.Commit storage commit = roundCommits[commitKeys[i]];
            if (!commit.revealed && commit.stakeAmount > 0) {
                uint256 amount = commit.stakeAmount;
                commit.stakeAmount = 0;

                if (round.state == RoundLib.RoundState.RevealFailed || commit.revealableAfter <= round.settledAt) {
                    processedPastEpochCount++;
                    if (round.state == RoundLib.RoundState.Settled) {
                        addedToConsensusReserve += amount;
                        updatedConsensusReserve += amount;
                    } else {
                        forfeitedToTreasury += amount;
                    }
                } else {
                    try TokenTransferLib.safeTransfer(hrepToken, commit.voter, amount) {
                        refundedHrep += amount;
                    } catch {
                        forfeitedToTreasury += amount;
                    }
                }
            }
        }

        uint256 cleanupIncentivePaid = roundCleanupIncentivePaid[contentId][roundId];
        cleanupIncentive = _cleanupIncentive(forfeitedToTreasury + addedToConsensusReserve, 5e6 - cleanupIncentivePaid);
        if (cleanupIncentive > 0) {
            roundCleanupIncentivePaid[contentId][roundId] = cleanupIncentivePaid + cleanupIncentive;
            uint256 fromReserve = addedToConsensusReserve < cleanupIncentive ? addedToConsensusReserve : cleanupIncentive;
            if (fromReserve > 0) {
                addedToConsensusReserve -= fromReserve;
                updatedConsensusReserve -= fromReserve;
            }
            uint256 fromTreasuryForfeiture = cleanupIncentive - fromReserve;
            if (fromTreasuryForfeiture > 0) {
                forfeitedToTreasury -= fromTreasuryForfeiture;
            }
            hrepToken.safeTransfer(cleanupCaller, cleanupIncentive);
        }

        if (forfeitedToTreasury > 0) {
            address currentTreasury = protocolConfig.treasury();
            if (currentTreasury != address(0)) {
                try TokenTransferLib.safeTransfer(hrepToken, currentTreasury, forfeitedToTreasury) {}
                catch {
                    updatedConsensusReserve += forfeitedToTreasury;
                }
            } else {
                updatedConsensusReserve += forfeitedToTreasury;
            }
        }

        if (forfeitedToTreasury == 0 && addedToConsensusReserve == 0 && refundedHrep == 0 && cleanupIncentive == 0) {
            revert NothingProcessed();
        }
    }

    function _cleanupIncentive(uint256 forfeitedAmount, uint256 remainingCap) private pure returns (uint256 incentive) {
        if (remainingCap == 0) return 0;
        incentive = forfeitedAmount * CLEANUP_INCENTIVE_BPS / 10_000;
        if (incentive > CLEANUP_INCENTIVE_MAX) incentive = CLEANUP_INCENTIVE_MAX;
        if (incentive > remainingCap) incentive = remainingCap;
    }
}
