// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import { Initializable } from "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import { AccessControlUpgradeable } from "@openzeppelin/contracts-upgradeable/access/AccessControlUpgradeable.sol";
import { PausableUpgradeable } from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import { ReentrancyGuardTransient } from "@openzeppelin/contracts/utils/ReentrancyGuardTransient.sol";
import { IERC20 } from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import { SafeERC20 } from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import { SafeCast } from "@openzeppelin/contracts/utils/math/SafeCast.sol";
import { ICategoryRegistry } from "./interfaces/ICategoryRegistry.sol";
import { IRoundVotingEngine } from "./interfaces/IRoundVotingEngine.sol";
import { IRaterIdentityRegistry } from "./interfaces/IRaterIdentityRegistry.sol";
import { RoundLib } from "./libraries/RoundLib.sol";
import { RatingLib } from "./libraries/RatingLib.sol";
import { RatingMath } from "./libraries/RatingMath.sol";
import { ProtocolConfig } from "./ProtocolConfig.sol";
import { SubmissionMediaValidator } from "./SubmissionMediaValidator.sol";

interface IQuestionRewardPoolEscrow {
    function createSubmissionRewardPoolFromRegistry(
        uint256 contentId,
        address funder,
        address payer,
        uint8 asset,
        uint256 amount,
        uint256 requiredVoters,
        uint256 requiredSettledRounds,
        uint256 bountyClosesAt,
        uint256 feedbackClosesAt,
        uint8 bountyEligibility
    ) external returns (uint256 rewardPoolId);

    function createSubmissionBundleFromRegistry(
        uint256 bundleId,
        uint256[] calldata contentIds,
        address funder,
        uint8 asset,
        uint256 amount,
        uint256 requiredCompleters,
        uint256 requiredSettledRounds,
        uint256 bountyClosesAt,
        uint256 feedbackClosesAt,
        uint8 bountyEligibility
    ) external returns (uint256 rewardPoolId);
}

/// @title ContentRegistry
/// @notice Manages content lifecycle: submission → active → dormant → revived / cancelled.
/// @dev Stores only a metadata hash on-chain; full URL/question/description are emitted in events.
contract ContentRegistry is Initializable, AccessControlUpgradeable, PausableUpgradeable, ReentrancyGuardTransient {
    using SafeERC20 for IERC20;
    using SafeCast for uint256;

    error OnlyVotingEngine();
    error ActiveRoundOnPreviousEngine();

    // --- Access Control Roles ---
    bytes32 public constant CONFIG_ROLE = keccak256("CONFIG_ROLE");
    bytes32 public constant PAUSER_ROLE = keccak256("PAUSER_ROLE");
    bytes32 public constant TREASURY_ROLE = keccak256("TREASURY_ROLE");
    bytes32 public constant TREASURY_ADMIN_ROLE = keccak256("TREASURY_ADMIN_ROLE");
    bytes32 public constant X402_GATEWAY_ROLE = keccak256("X402_GATEWAY_ROLE");

    // --- Constants ---
    uint256 internal constant REVIVAL_STAKE = 5e6; // 5 LREP (6 decimals)
    uint256 internal constant DORMANCY_PERIOD = 30 days;
    uint256 internal constant SUBMISSION_RESERVATION_PERIOD = 30 minutes;
    uint256 internal constant RESERVED_SUBMISSION_MIN_AGE = 1 seconds;
    uint256 internal constant DORMANT_EXCLUSIVE_REVIVAL_PERIOD = 1 days;
    uint8 internal constant MAX_REVIVALS = 2;
    uint8 internal constant SUBMISSION_REWARD_ASSET_LREP = 0;
    uint8 internal constant SUBMISSION_REWARD_ASSET_USDC = 1;
    uint256 internal constant DEFAULT_MIN_SUBMISSION_REWARD_POOL = 1e6;
    uint256 internal constant MIN_SUBMISSION_REWARD_REQUIRED_VOTERS = 3;
    uint256 internal constant MIN_SUBMISSION_REWARD_SETTLED_ROUNDS = 1;
    uint256 internal constant MAX_SUBMISSION_REWARD_SETTLED_ROUNDS = 16;
    uint256 internal constant MAX_QUESTION_BUNDLE_COUNT = 10;

    // Rating safety thresholds
    uint256 internal constant SLASH_RATING_THRESHOLD = 25; // Rating below this triggers slash
    uint16 internal constant DEFAULT_SLASH_THRESHOLD_BPS = 2500;
    uint16 internal constant DEFAULT_MIN_SLASH_SETTLED_ROUNDS = 2;
    uint48 internal constant DEFAULT_MIN_SLASH_LOW_DURATION = 7 days;
    uint256 internal constant DEFAULT_MIN_SLASH_EVIDENCE = 200e6;
    uint256 internal constant DEFAULT_CONFIDENCE_MASS_INITIAL = 80e6;

    // String length limits (prevent storage bloat)
    uint256 internal constant MAX_URL_LENGTH = 2048;
    uint256 internal constant MAX_QUESTION_LENGTH = 120;
    uint256 internal constant MAX_DESCRIPTION_LENGTH = 280;
    uint256 internal constant MAX_TAGS_LENGTH = 256;
    uint256 internal constant MAX_IMAGE_URLS = 4;
    uint16 internal constant MAX_QUESTION_BUNDLE_ROUND_VOTERS = 100;

    // --- Enums ---
    enum ContentStatus {
        Active,
        Dormant,
        Cancelled
    }

    // --- Structs ---
    struct Content {
        uint64 id;
        bytes32 contentHash;
        address submitter;
        uint48 createdAt;
        uint48 lastActivityAt;
        ContentStatus status;
        uint8 dormantCount;
        address reviver;
        uint8 rating; // 0-100, starts at 50
        uint64 categoryId; // Reference to seeded discovery category
    }

    struct PendingSubmission {
        address submitter;
        uint48 reservedAt;
        uint48 expiresAt;
    }

    struct SubmissionMetadata {
        string url;
        string title;
        string description;
        string tags;
        uint256 categoryId;
    }

    struct SubmissionRewardTerms {
        uint8 asset;
        uint256 amount;
        uint256 requiredVoters;
        uint256 requiredSettledRounds;
        uint256 bountyClosesAt;
        uint256 feedbackClosesAt;
        uint8 bountyEligibility;
    }

    struct QuestionSpecCommitment {
        bytes32 questionMetadataHash;
        bytes32 resultSpecHash;
    }

    struct BundleQuestionInput {
        string contextUrl;
        string[] imageUrls;
        string videoUrl;
        string title;
        string description;
        string tags;
        uint256 categoryId;
        bytes32 salt;
        QuestionSpecCommitment spec;
    }

    // --- State ---
    IERC20 public lrepToken;
    address public votingEngine;
    ICategoryRegistry public categoryRegistry;
    address public bonusPool; // Cancellation fee sink (anti-spam), typically the treasury
    address public treasury; // Receives 100% of slashed stakes (governance timelock)
    uint256 public nextContentId;
    uint256 internal nextQuestionBundleId;
    mapping(uint256 => Content) public contents;
    mapping(bytes32 => bool) public submissionKeyUsed; // Canonical submission keys prevent duplicate content variants
    /// @notice Escrow that holds mandatory bounties.
    address public questionRewardPoolEscrow;
    uint256 internal votingEngineGeneration;
    mapping(address => uint256) internal votingEngineCallbackGeneration;
    mapping(uint256 => uint256) internal contentSettlementEngineGeneration;

    /// @notice Canonical submission key per content ID (for releasing/reserving uniqueness on status changes)
    mapping(uint256 => bytes32) internal contentSubmissionKey;

    /// @notice Canonical submitter identity snapshot (holder address if submitted through a delegate).
    /// @dev Public to expose the auto-getter as `getSubmitterIdentity(uint256)` and skip the
    ///      explicit external wrapper, keeping ContentRegistry under the EIP-170 size limit.
    mapping(uint256 => address) public getSubmitterIdentity;

    /// @notice Stable rater identity key for the canonical submitter identity.
    mapping(uint256 => bytes32) public contentSubmitterIdentityKey;

    /// @notice Per-question round settings selected at submission and bounded by governance.
    mapping(uint256 => RoundLib.RoundConfig) internal contentRoundConfig;

    mapping(uint256 => uint256) internal contentBundleId;

    /// @notice Meaningful-activity anchor used for dormancy checks.
    /// @dev Vote commits still update `lastActivityAt` for UI/analytics, but only submission, revival,
    ///      and milestone-0 settlement move the dormancy window forward.
    mapping(uint256 => uint256) internal dormancyAnchorAt;

    /// @notice Voting engine whose current round may still need to block dormancy for this content.
    mapping(uint256 => address) internal contentRoundTrackingEngine;

    /// @notice ProtocolConfig used for governance-tunable rating and slash parameters.
    ProtocolConfig public protocolConfig;

    /// @notice Hidden, time-bounded reservations for future content reveals.
    mapping(bytes32 => PendingSubmission) internal pendingSubmissions;

    /// @notice Timestamp after which a dormant content key may be publicly released for replacement.
    mapping(uint256 => uint256) public dormantKeyReleasableAt;

    /// @notice Rich rating state used by the score-relative rating system.
    mapping(uint256 => RatingLib.RatingState) internal _ratingState;

    /// @notice Slash policy frozen at content creation so governance cannot retroactively rewrite stake terms.
    mapping(uint256 => RatingLib.SlashConfig) internal contentSlashConfigSnapshot;

    SubmissionMediaValidator internal immutable SUBMISSION_MEDIA_VALIDATOR;

    /// @dev Reserved storage gap for future upgrades
    uint256[42] private __gap;

    // --- Events ---
    event ContentSubmitted(
        uint256 indexed contentId,
        address indexed submitter,
        bytes32 contentHash,
        string url,
        string title,
        string description,
        string tags,
        uint256 indexed categoryId
    );
    event QuestionSpecAnchored(uint256 indexed contentId, bytes32 questionMetadataHash, bytes32 resultSpecHash);
    event ContentMediaSubmitted(uint256 indexed contentId, string[] imageUrls, string videoUrl);
    event ContentCancelled(uint256 indexed contentId);
    event SubmissionReserved(address indexed submitter, bytes32 indexed revealCommitment, uint256 expiresAt);
    event SubmissionReservationCancelled(address indexed submitter, bytes32 indexed revealCommitment);
    event SubmissionReservationExpired(address indexed submitter, bytes32 indexed revealCommitment);
    event SubmissionRewardPoolAttached(
        uint256 indexed contentId,
        address indexed submitter,
        uint8 indexed rewardAsset,
        uint256 amount,
        uint256 requiredVoters,
        uint256 requiredSettledRounds,
        uint256 bountyClosesAt,
        uint256 feedbackClosesAt,
        uint8 bountyEligibility,
        bytes32 bountyEligibilityDataHash,
        uint256 rewardPoolId
    );
    event QuestionBundleSubmitted(
        uint256 indexed bundleId,
        address indexed submitter,
        uint256 questionCount,
        uint8 indexed rewardAsset,
        uint256 amount,
        uint256 requiredCompleters,
        uint256 bountyClosesAt,
        uint256 feedbackClosesAt,
        uint8 bountyEligibility,
        bytes32 bountyEligibilityDataHash,
        bytes32 bundleHash,
        uint256 rewardPoolId
    );
    event QuestionBundleContentLinked(uint256 indexed bundleId, uint256 indexed contentId, uint256 indexed bundleIndex);
    event ContentDormant(uint256 indexed contentId);
    event DormantSubmissionKeyReleased(uint256 indexed contentId, bytes32 indexed submissionKey);
    event ContentRevived(uint256 indexed contentId, address indexed reviver);
    event RatingUpdated(uint256 indexed contentId, uint256 oldRating, uint256 newRating);
    event RatingStateUpdated(
        uint256 indexed contentId,
        uint256 indexed roundId,
        uint16 referenceRatingBps,
        uint16 oldRatingBps,
        uint16 newRatingBps,
        uint16 conservativeRatingBps,
        uint256 confidenceMass,
        uint256 effectiveEvidence,
        uint32 settledRounds
    );
    event QuestionRewardPoolEscrowUpdated(address rewardPoolEscrow);
    event ContentRoundConfigSet(
        uint256 indexed contentId, uint32 epochDuration, uint32 maxDuration, uint16 minVoters, uint16 maxVoters
    );
    event VotingEngineRevoked(address indexed engine);

    /// @custom:oz-upgrades-unsafe-allow constructor state-variable-immutable
    constructor() {
        SUBMISSION_MEDIA_VALIDATOR = new SubmissionMediaValidator();
        _disableInitializers();
    }

    function initializeWithTreasury(address _admin, address _governance, address _treasuryAuthority, address _lrepToken)
        public
        initializer
    {
        _initialize(_admin, _governance, _treasuryAuthority, _lrepToken);
    }

    function _initialize(address _admin, address _governance, address _treasuryAuthority, address _lrepToken) internal {
        __AccessControl_init();
        __Pausable_init();

        require(_admin != address(0), "Invalid admin");
        require(_governance != address(0), "Invalid governance");
        require(_treasuryAuthority != address(0), "Bad treasury");
        require(_lrepToken != address(0), "Invalid LREP token");

        // Governance gets all permanent roles
        _grantRole(DEFAULT_ADMIN_ROLE, _governance);
        _grantRole(CONFIG_ROLE, _governance);
        _grantRole(PAUSER_ROLE, _governance);
        _setRoleAdmin(TREASURY_ROLE, TREASURY_ADMIN_ROLE);
        _setRoleAdmin(TREASURY_ADMIN_ROLE, TREASURY_ADMIN_ROLE);
        _setRoleAdmin(X402_GATEWAY_ROLE, CONFIG_ROLE);
        _grantRole(TREASURY_ADMIN_ROLE, _governance);
        _grantRole(TREASURY_ADMIN_ROLE, _treasuryAuthority);
        _grantRole(TREASURY_ROLE, _treasuryAuthority);

        // Admin gets only CONFIG_ROLE for initial cross-contract wiring
        if (_admin != _governance) {
            _grantRole(CONFIG_ROLE, _admin);
        }

        lrepToken = IERC20(_lrepToken);
        nextContentId = 1;
        nextQuestionBundleId = 1;
        treasury = _treasuryAuthority;
        bonusPool = _treasuryAuthority;
    }

    /// @notice Set the VotingEngine address (can only be called by CONFIG_ROLE).
    /// @dev Authorization for the previous engine is NOT cleared by this call; the canonical
    ///      check in `_authorizeEngineCallback` rejects any non-canonical caller, so a rotated
    ///      engine cannot mutate registry state. Operators should still follow up with
    ///      `revokeVotingEngine(prevEngine)` to clear the per-engine grant for hygiene and to
    ///      make off-chain monitoring of obsolete engines unambiguous.
    function setVotingEngine(address _votingEngine) external onlyRole(CONFIG_ROLE) {
        require(_votingEngine != address(0), "Invalid address");
        require(_votingEngine.code.length != 0, "No code");
        if (votingEngine == _votingEngine) return;
        require(votingEngine == address(0) || paused(), "Pause required");
        uint256 nextGeneration;
        unchecked {
            nextGeneration = votingEngineGeneration + 1;
        }
        votingEngine = _votingEngine;
        votingEngineGeneration = nextGeneration;
        votingEngineCallbackGeneration[_votingEngine] = nextGeneration;
    }

    /// @notice Revoke callback authorization for a previously-registered voting engine.
    /// @dev Belt-and-suspenders for the canonical-only check in `_authorizeEngineCallback`.
    ///      Use after `setVotingEngine` rotates to a new engine and any in-flight rounds on
    ///      the old engine have been drained. Cannot be used on the canonical engine — call
    ///      `setVotingEngine` to rotate first.
    function revokeVotingEngine(address engine) external onlyRole(CONFIG_ROLE) {
        require(engine != address(0), "Invalid address");
        require(engine != votingEngine, "Cannot revoke canonical engine");
        require(votingEngineCallbackGeneration[engine] != 0, "Engine not authorized");
        delete votingEngineCallbackGeneration[engine];
        emit VotingEngineRevoked(engine);
    }

    /// @notice Set the CategoryRegistry address (can only be called by CONFIG_ROLE).
    function setCategoryRegistry(address _categoryRegistry) external onlyRole(CONFIG_ROLE) {
        require(_categoryRegistry != address(0), "Invalid address");
        categoryRegistry = ICategoryRegistry(_categoryRegistry);
    }

    function setProtocolConfig(address _protocolConfig) external onlyRole(CONFIG_ROLE) {
        require(_protocolConfig != address(0), "Invalid address");
        protocolConfig = ProtocolConfig(_protocolConfig);
    }

    /// @notice Set or update the bounty escrow.
    function setQuestionRewardPoolEscrow(address _questionRewardPoolEscrow) external onlyRole(CONFIG_ROLE) {
        require(_questionRewardPoolEscrow != address(0), "Invalid address");
        require(_questionRewardPoolEscrow.code.length != 0, "No code");
        if (questionRewardPoolEscrow == _questionRewardPoolEscrow) return;
        require(questionRewardPoolEscrow == address(0) || paused(), "Pause required");
        questionRewardPoolEscrow = _questionRewardPoolEscrow;
        emit QuestionRewardPoolEscrowUpdated(_questionRewardPoolEscrow);
    }

    /// @notice Set the cancellation fee sink address (can only be called by TREASURY_ROLE).
    function setBonusPool(address _bonusPool) external onlyRole(TREASURY_ROLE) {
        require(_bonusPool != address(0), "Invalid address");
        bonusPool = _bonusPool;
    }

    /// @notice Set the treasury address that receives slashed stakes (can only be called by TREASURY_ROLE).
    function setTreasury(address _treasury) external onlyRole(TREASURY_ROLE) {
        require(_treasury != address(0), "Invalid address");
        treasury = _treasury;
    }

    // --- Content Lifecycle ---

    /// @notice Compute the internal storage key for a reservation.
    /// @dev Scoping the mapping key by `submitter` prevents a front-runner from copying a
    ///      victim's revealCommitment out of the mempool and squatting the reservation slot.
    ///      Each submitter has their own namespace, so collisions are impossible.
    function _reservationKey(bytes32 revealCommitment, address submitter) internal pure returns (bytes32) {
        return keccak256(abi.encode(revealCommitment, submitter));
    }

    /// @notice Reserve a hidden submission commitment before revealing the public content metadata.
    /// @param revealCommitment Keccak-256 hash of the future submission reveal payload.
    function reserveSubmission(bytes32 revealCommitment) external nonReentrant whenNotPaused {
        require(revealCommitment != bytes32(0), "Invalid commitment");
        bytes32 key = _reservationKey(revealCommitment, msg.sender);
        PendingSubmission storage pending = pendingSubmissions[key];
        require(pending.submitter == address(0), "Reservation exists");

        pendingSubmissions[key] = PendingSubmission({
            submitter: msg.sender,
            reservedAt: block.timestamp.toUint48(),
            expiresAt: (block.timestamp + SUBMISSION_RESERVATION_PERIOD).toUint48()
        });

        emit SubmissionReserved(msg.sender, revealCommitment, block.timestamp + SUBMISSION_RESERVATION_PERIOD);
    }

    function cancelReservedSubmission(bytes32 revealCommitment) external nonReentrant whenNotPaused {
        bytes32 key = _reservationKey(revealCommitment, msg.sender);
        PendingSubmission memory pending = pendingSubmissions[key];
        require(pending.submitter == msg.sender, "Not submitter");
        delete pendingSubmissions[key];

        emit SubmissionReservationCancelled(msg.sender, revealCommitment);
    }

    function clearExpiredReservedSubmission(bytes32 revealCommitment) external nonReentrant whenNotPaused {
        // Caller sweeps their own expired reservation. Because keys are scoped by submitter,
        // other users cannot accidentally interfere with each other's expirations.
        bytes32 key = _reservationKey(revealCommitment, msg.sender);
        PendingSubmission memory pending = pendingSubmissions[key];
        require(pending.submitter != address(0), "Reservation not found");
        require(block.timestamp > pending.expiresAt, "Reservation active");
        delete pendingSubmissions[key];

        emit SubmissionReservationExpired(pending.submitter, revealCommitment);
    }

    function submitQuestionWithRewardAndRoundConfig(
        string memory contextUrl,
        string[] memory imageUrls,
        string memory videoUrl,
        string memory title,
        string memory description,
        string memory tags,
        uint256 categoryId,
        bytes32 salt,
        SubmissionRewardTerms memory rewardTerms,
        RoundLib.RoundConfig memory roundConfig,
        QuestionSpecCommitment memory spec
    ) public nonReentrant whenNotPaused returns (uint256) {
        SubmissionMetadata memory metadata = _validatedContextSubmissionMetadata(
            contextUrl, imageUrls, videoUrl, title, description, tags, categoryId
        );
        return _submitValidatedQuestionWithMedia(
            metadata, imageUrls, videoUrl, salt, rewardTerms, _validatedRoundConfig(roundConfig), 0, spec
        );
    }

    function submitQuestionBundleWithRewardAndRoundConfig(
        BundleQuestionInput[] calldata questions,
        SubmissionRewardTerms calldata rewardTerms,
        RoundLib.RoundConfig calldata roundConfig
    ) external nonReentrant whenNotPaused returns (uint256 bundleId, uint256[] memory contentIds) {
        require(questions.length > 0, "No questions");
        require(questions.length > 1, "Bundle needs multiple questions");
        require(questions.length <= MAX_QUESTION_BUNDLE_COUNT, "Too many questions");
        require(questionRewardPoolEscrow != address(0), "Bounty escrow not set");

        RoundLib.RoundConfig memory validatedRoundConfig = _validatedRoundConfig(roundConfig);
        require(validatedRoundConfig.maxVoters <= MAX_QUESTION_BUNDLE_ROUND_VOTERS);
        require(rewardTerms.requiredVoters <= validatedRoundConfig.maxVoters);
        _validateSubmissionReward(rewardTerms);
        require(rewardTerms.bountyClosesAt != 0, "Bundle bounty close required");

        SubmissionMetadata[] memory metadataList = new SubmissionMetadata[](questions.length);
        bytes32[] memory submissionKeys = new bytes32[](questions.length);
        bytes32[] memory mediaHashes = new bytes32[](questions.length);
        uint256[] memory resolvedCategoryIds = new uint256[](questions.length);

        for (uint256 i = 0; i < questions.length; i++) {
            BundleQuestionInput calldata question = questions[i];
            _validateQuestionSpec(question.spec);
            SubmissionMetadata memory metadata = _validatedContextSubmissionMetadata(
                question.contextUrl,
                question.imageUrls,
                question.videoUrl,
                question.title,
                question.description,
                question.tags,
                question.categoryId
            );
            uint256 resolvedCategoryId = _resolveQuestionSubmissionCategory(metadata);
            bytes32 submissionKey = _deriveQuestionMediaSubmissionKey(metadata, resolvedCategoryId);
            // Eager write in this loop also catches duplicates WITHIN the same bundle --
            // without it, two identical questions in one bundle would both pass the
            // check here and later point at the same submissionKey, so releasing one
            // in Dormant state would brick the sibling forever ("Dormant key released").
            require(!submissionKeyUsed[submissionKey], "Question already submitted");
            submissionKeyUsed[submissionKey] = true;
            // A zero salt collapses the reveal commitment's entropy and lets a front-runner
            // pre-reserve the same (metadata, ..., salt=0) tuple. Force non-zero salt per
            // question so every reveal hash carries caller-supplied randomness.
            require(question.salt != bytes32(0), "Salt required");

            metadataList[i] = metadata;
            submissionKeys[i] = submissionKey;
            mediaHashes[i] = _submissionMediaHash(question.imageUrls, question.videoUrl);
            resolvedCategoryIds[i] = resolvedCategoryId;
        }

        bytes32 bundleHash = _computeQuestionBundleHash(metadataList, mediaHashes, resolvedCategoryIds, questions);
        bytes32 revealCommitment =
            _computeBundleRevealCommitment(bundleHash, msg.sender, rewardTerms, validatedRoundConfig);
        bytes32 reservationKey = _reservationKey(revealCommitment, msg.sender);
        PendingSubmission memory pending = pendingSubmissions[reservationKey];
        require(pending.submitter == msg.sender, "Reservation not found");
        require(block.timestamp <= pending.expiresAt, "Reservation expired");
        require(block.timestamp >= pending.reservedAt + RESERVED_SUBMISSION_MIN_AGE, "Reservation too new");
        delete pendingSubmissions[reservationKey];

        bundleId = nextQuestionBundleId++;
        contentIds = new uint256[](questions.length);
        for (uint256 i = 0; i < questions.length; i++) {
            // submissionKeyUsed is already set in the preparation loop above to catch
            // duplicates within the same bundle.
            bytes32 contentHash = keccak256(
                abi.encode(
                    "curyo-question-context-v2",
                    metadataList[i].url,
                    questions[i].imageUrls,
                    questions[i].videoUrl,
                    metadataList[i].title,
                    metadataList[i].description,
                    metadataList[i].tags,
                    resolvedCategoryIds[i],
                    questions[i].spec.questionMetadataHash,
                    questions[i].spec.resultSpecHash
                )
            );
            uint256 contentId = _storeSubmittedContent(
                submissionKeys[i],
                pending.submitter,
                contentHash,
                resolvedCategoryIds[i],
                validatedRoundConfig,
                bundleId
            );
            contentIds[i] = contentId;

            emit ContentSubmitted(
                contentId,
                msg.sender,
                contentHash,
                metadataList[i].url,
                metadataList[i].title,
                metadataList[i].description,
                metadataList[i].tags,
                resolvedCategoryIds[i]
            );
            emit QuestionSpecAnchored(
                contentId, questions[i].spec.questionMetadataHash, questions[i].spec.resultSpecHash
            );
            emit ContentMediaSubmitted(contentId, questions[i].imageUrls, questions[i].videoUrl);
            emit QuestionBundleContentLinked(bundleId, contentId, i);
        }

        uint256 rewardPoolId = IQuestionRewardPoolEscrow(questionRewardPoolEscrow)
            .createSubmissionBundleFromRegistry(
                bundleId,
                contentIds,
                msg.sender,
                rewardTerms.asset,
                rewardTerms.amount,
                rewardTerms.requiredVoters,
                rewardTerms.requiredSettledRounds,
                rewardTerms.bountyClosesAt,
                rewardTerms.feedbackClosesAt,
                rewardTerms.bountyEligibility
            );
        emit QuestionBundleSubmitted(
            bundleId,
            msg.sender,
            questions.length,
            rewardTerms.asset,
            rewardTerms.amount,
            rewardTerms.requiredVoters,
            rewardTerms.bountyClosesAt,
            rewardTerms.feedbackClosesAt,
            rewardTerms.bountyEligibility,
            bytes32(0),
            bundleHash,
            rewardPoolId
        );
    }

    /// @notice Submit a question with a context link or image evidence.
    /// @dev Attaches the governance minimum LREP bounty and default round configuration.
    function submitQuestion(
        string memory contextUrl,
        string[] memory imageUrls,
        string memory videoUrl,
        string memory title,
        string memory description,
        string memory tags,
        uint256 categoryId,
        bytes32 salt,
        QuestionSpecCommitment memory spec
    ) public nonReentrant whenNotPaused returns (uint256) {
        SubmissionRewardTerms memory rewardTerms = SubmissionRewardTerms({
            asset: SUBMISSION_REWARD_ASSET_LREP,
            amount: _minimumSubmissionReward(SUBMISSION_REWARD_ASSET_LREP),
            requiredVoters: MIN_SUBMISSION_REWARD_REQUIRED_VOTERS,
            requiredSettledRounds: MIN_SUBMISSION_REWARD_SETTLED_ROUNDS,
            bountyClosesAt: 0,
            feedbackClosesAt: 0,
            bountyEligibility: 0
        });
        SubmissionMetadata memory metadata =
            _validatedContextSubmissionMetadata(contextUrl, imageUrls, videoUrl, title, description, tags, categoryId);

        return _submitValidatedQuestionWithMedia(
            metadata, imageUrls, videoUrl, salt, rewardTerms, _defaultRoundConfig(), 0, spec
        );
    }

    function submitQuestionFromX402Gateway(
        string memory contextUrl,
        string[] memory imageUrls,
        string memory videoUrl,
        string memory title,
        string memory description,
        string memory tags,
        uint256 categoryId,
        bytes32 salt,
        SubmissionRewardTerms memory rewardTerms,
        RoundLib.RoundConfig memory roundConfig,
        QuestionSpecCommitment memory spec,
        address submitter
    ) external onlyRole(X402_GATEWAY_ROLE) nonReentrant whenNotPaused returns (uint256 contentId) {
        require(submitter != address(0), "Invalid submitter");
        require(rewardTerms.asset == SUBMISSION_REWARD_ASSET_USDC, "USDC required");
        SubmissionMetadata memory metadata =
            _validatedContextSubmissionMetadata(contextUrl, imageUrls, videoUrl, title, description, tags, categoryId);
        RoundLib.RoundConfig memory validatedRoundConfig = _validatedRoundConfig(roundConfig);
        (uint256 resolvedCategoryId, bytes32 submissionKey,) = _prepareQuestionMediaSubmission(
            metadata, imageUrls, videoUrl, salt, rewardTerms, validatedRoundConfig, spec, submitter
        );
        contentId = _storeQuestionAndAttachReward(
            submissionKey,
            submitter,
            msg.sender,
            metadata,
            imageUrls,
            videoUrl,
            resolvedCategoryId,
            rewardTerms,
            validatedRoundConfig,
            0,
            spec
        );
    }

    function getContentRoundConfig(uint256 contentId) public view returns (RoundLib.RoundConfig memory cfg) {
        cfg = contentRoundConfig[contentId];
        if (cfg.epochDuration == 0) {
            cfg = _defaultRoundConfig();
        }
    }

    /// @notice Cancel content before any votes. Attached submission bounties stay non-refundable.
    /// @dev Only callable by the submitter. VotingEngine must confirm 0 votes.
    function cancelContent(uint256 contentId) external nonReentrant whenNotPaused {
        require(bonusPool != address(0), "Bonus pool not set");
        Content storage c = contents[contentId];
        require(_isSubmitterIdentity(contentId, msg.sender), "Not submitter");
        require(c.status == ContentStatus.Active, "Not active");
        require(contentRoundTrackingEngine[contentId] == address(0), "Content has votes");
        if (votingEngine != address(0)) {
            require(!IRoundVotingEngine(votingEngine).hasCommits(contentId), "Content has votes");
        }
        // Cancelling a bundle member would permanently prevent the bundle escrow from
        // completing all configured round sets. Treat bundles as atomic once submitted.
        require(contentBundleId[contentId] == 0, "Bundled content");

        c.status = ContentStatus.Cancelled;

        // Release the canonical submission key so the content can be resubmitted.
        bytes32 submissionKey = contentSubmissionKey[contentId];
        if (submissionKey != bytes32(0)) {
            submissionKeyUsed[submissionKey] = false;
        }

        emit ContentCancelled(contentId);
    }

    function _validatedContextSubmissionMetadata(
        string memory contextUrl,
        string[] memory imageUrls,
        string memory videoUrl,
        string memory title,
        string memory description,
        string memory tags,
        uint256 categoryId
    ) internal view returns (SubmissionMetadata memory metadata) {
        bool hasContextUrl = bytes(contextUrl).length != 0;
        if (hasContextUrl) {
            SUBMISSION_MEDIA_VALIDATOR.validateContextUrl(contextUrl);
        }
        SUBMISSION_MEDIA_VALIDATOR.validateOptionalMediaSet(imageUrls, videoUrl);
        require(hasContextUrl || imageUrls.length > 0, "Context or image required");
        metadata = SubmissionMetadata({
            url: contextUrl, title: title, description: description, tags: tags, categoryId: categoryId
        });
        _validateTextFields(metadata);
        require(address(categoryRegistry) != address(0), "Category unset");
    }

    function _validateTextFields(SubmissionMetadata memory metadata) internal pure {
        require(bytes(metadata.url).length <= MAX_URL_LENGTH, "URL too long");
        require(bytes(metadata.title).length > 0, "Question required");
        require(bytes(metadata.title).length <= MAX_QUESTION_LENGTH, "Question too long");
        require(bytes(metadata.description).length <= MAX_DESCRIPTION_LENGTH, "Description too long");
        require(bytes(metadata.tags).length > 0, "Tags required");
        require(bytes(metadata.tags).length <= MAX_TAGS_LENGTH, "Tags too long");
    }

    function _submitValidatedQuestionWithMedia(
        SubmissionMetadata memory metadata,
        string[] memory imageUrls,
        string memory videoUrl,
        bytes32 salt,
        SubmissionRewardTerms memory rewardTerms,
        RoundLib.RoundConfig memory roundConfig,
        uint256 bundleId,
        QuestionSpecCommitment memory spec
    ) internal returns (uint256 contentId) {
        (uint256 resolvedCategoryId, bytes32 submissionKey, PendingSubmission memory pending) = _prepareQuestionMediaSubmission(
            metadata, imageUrls, videoUrl, salt, rewardTerms, roundConfig, spec, msg.sender
        );
        contentId = _storeQuestionAndAttachReward(
            submissionKey,
            pending.submitter,
            msg.sender,
            metadata,
            imageUrls,
            videoUrl,
            resolvedCategoryId,
            rewardTerms,
            roundConfig,
            bundleId,
            spec
        );
    }

    function _prepareQuestionMediaSubmission(
        SubmissionMetadata memory metadata,
        string[] memory imageUrls,
        string memory videoUrl,
        bytes32 salt,
        SubmissionRewardTerms memory rewardTerms,
        RoundLib.RoundConfig memory roundConfig,
        QuestionSpecCommitment memory spec,
        address submitter
    ) internal returns (uint256 resolvedCategoryId, bytes32 submissionKey, PendingSubmission memory pending) {
        _validateQuestionSpec(spec);
        resolvedCategoryId = _resolveQuestionSubmissionCategory(metadata);
        submissionKey = _deriveQuestionMediaSubmissionKey(metadata, resolvedCategoryId);
        require(!submissionKeyUsed[submissionKey], "Question already submitted");
        // A zero salt collapses the reveal commitment's entropy and lets a front-runner
        // pre-reserve the same (metadata, ..., salt=0) tuple. Force non-zero salt so every
        // reveal hash carries caller-supplied randomness.
        require(salt != bytes32(0), "Salt required");
        _validateSubmissionReward(rewardTerms);
        require(rewardTerms.requiredVoters <= roundConfig.maxVoters, "Voters exceed max");

        bytes32 mediaHash = _submissionMediaHash(imageUrls, videoUrl);
        bytes32 revealCommitment = _computeRevealCommitment(
            submissionKey,
            mediaHash,
            metadata.title,
            metadata.description,
            metadata.tags,
            metadata.categoryId,
            salt,
            submitter,
            rewardTerms,
            roundConfig,
            spec
        );
        pending = _consumeReservedSubmission(revealCommitment, submitter);
        submissionKeyUsed[submissionKey] = true;
    }

    function _consumeReservedSubmission(bytes32 revealCommitment, address submitter)
        internal
        returns (PendingSubmission memory pending)
    {
        bytes32 reservationKey = _reservationKey(revealCommitment, submitter);
        pending = pendingSubmissions[reservationKey];
        require(pending.submitter == submitter, "Reservation not found");
        require(block.timestamp <= pending.expiresAt, "Reservation expired");
        require(block.timestamp >= pending.reservedAt + RESERVED_SUBMISSION_MIN_AGE, "Reservation too new");

        delete pendingSubmissions[reservationKey];
    }

    function _submissionMediaHash(string[] memory imageUrls, string memory videoUrl) internal pure returns (bytes32) {
        return keccak256(abi.encode(imageUrls, videoUrl));
    }

    function _questionContentHash(
        SubmissionMetadata memory metadata,
        string[] memory imageUrls,
        string memory videoUrl,
        uint256 resolvedCategoryId,
        QuestionSpecCommitment memory spec
    ) internal pure returns (bytes32) {
        return keccak256(
            abi.encode(
                "curyo-question-context-v2",
                metadata.url,
                imageUrls,
                videoUrl,
                metadata.title,
                metadata.description,
                metadata.tags,
                resolvedCategoryId,
                spec.questionMetadataHash,
                spec.resultSpecHash
            )
        );
    }

    function _validateQuestionSpec(QuestionSpecCommitment memory spec) internal pure {
        require(spec.questionMetadataHash != bytes32(0), "Bad metadata");
        require(spec.resultSpecHash != bytes32(0), "Bad spec");
    }

    function _resolveQuestionSubmissionCategory(SubmissionMetadata memory metadata)
        internal
        view
        returns (uint256 resolvedCategoryId)
    {
        require(metadata.categoryId != 0, "Category required");
        require(categoryRegistry.isCategory(metadata.categoryId), "Category not registered");
        return metadata.categoryId;
    }

    function _deriveQuestionMediaSubmissionKey(SubmissionMetadata memory metadata, uint256 resolvedCategoryId)
        internal
        pure
        returns (bytes32)
    {
        return keccak256(
            abi.encode(
                "curyo-question-context-v1",
                resolvedCategoryId,
                metadata.url,
                metadata.title,
                metadata.description,
                metadata.tags
            )
        );
    }

    function _storeSubmittedContent(
        bytes32 submissionKey,
        address submitter,
        bytes32 contentHash,
        uint256 resolvedCategoryId,
        RoundLib.RoundConfig memory roundConfig,
        uint256 bundleId
    ) internal returns (uint256 contentId) {
        contentId = nextContentId++;
        contentSubmissionKey[contentId] = submissionKey;
        (address submitterIdentity, bytes32 submitterIdentityKey) = _snapshotSubmitterIdentity(submitter);
        getSubmitterIdentity[contentId] = submitterIdentity;
        contentSubmitterIdentityKey[contentId] = submitterIdentityKey;
        contentRoundConfig[contentId] = roundConfig;
        contents[contentId] = Content({
            id: contentId.toUint64(),
            contentHash: contentHash,
            submitter: submitter,
            createdAt: block.timestamp.toUint48(),
            lastActivityAt: block.timestamp.toUint48(),
            status: ContentStatus.Active,
            dormantCount: 0,
            reviver: address(0),
            rating: 50,
            categoryId: resolvedCategoryId.toUint64()
        });
        if (bundleId != 0) {
            contentBundleId[contentId] = bundleId;
        }
        emit ContentRoundConfigSet(
            contentId, roundConfig.epochDuration, roundConfig.maxDuration, roundConfig.minVoters, roundConfig.maxVoters
        );
        _ratingState[contentId] = RatingLib.RatingState({
            ratingLogitX18: int128(RatingLib.DEFAULT_RATING_LOGIT_X18),
            confidenceMass: uint128(_getInitialConfidenceMass()),
            effectiveEvidence: 0,
            settledRounds: 0,
            ratingBps: RatingLib.DEFAULT_RATING_BPS,
            conservativeRatingBps: RatingLib.DEFAULT_RATING_BPS,
            lastUpdatedAt: 0,
            lowSince: 0
        });
        contentSlashConfigSnapshot[contentId] = _getCurrentSlashConfig();
        dormancyAnchorAt[contentId] = block.timestamp;
        delete dormantKeyReleasableAt[contentId];
    }

    function _storeQuestionAndAttachReward(
        bytes32 submissionKey,
        address submitter,
        address funder,
        SubmissionMetadata memory metadata,
        string[] memory imageUrls,
        string memory videoUrl,
        uint256 resolvedCategoryId,
        SubmissionRewardTerms memory rewardTerms,
        RoundLib.RoundConfig memory roundConfig,
        uint256 bundleId,
        QuestionSpecCommitment memory spec
    ) internal returns (uint256 contentId) {
        bytes32 contentHash = _questionContentHash(metadata, imageUrls, videoUrl, resolvedCategoryId, spec);
        contentId =
            _storeSubmittedContent(submissionKey, submitter, contentHash, resolvedCategoryId, roundConfig, bundleId);
        require(questionRewardPoolEscrow != address(0), "Bounty escrow not set");
        uint256 rewardPoolId = IQuestionRewardPoolEscrow(questionRewardPoolEscrow)
            .createSubmissionRewardPoolFromRegistry(
                contentId,
                funder,
                submitter,
                rewardTerms.asset,
                rewardTerms.amount,
                rewardTerms.requiredVoters,
                rewardTerms.requiredSettledRounds,
                rewardTerms.bountyClosesAt,
                rewardTerms.feedbackClosesAt,
                rewardTerms.bountyEligibility
            );

        emit ContentSubmitted(
            contentId,
            submitter,
            contentHash,
            metadata.url,
            metadata.title,
            metadata.description,
            metadata.tags,
            resolvedCategoryId
        );
        emit QuestionSpecAnchored(contentId, spec.questionMetadataHash, spec.resultSpecHash);
        emit ContentMediaSubmitted(contentId, imageUrls, videoUrl);
        emit SubmissionRewardPoolAttached(
            contentId,
            submitter,
            rewardTerms.asset,
            rewardTerms.amount,
            rewardTerms.requiredVoters,
            rewardTerms.requiredSettledRounds,
            rewardTerms.bountyClosesAt,
            rewardTerms.feedbackClosesAt,
            rewardTerms.bountyEligibility,
            bytes32(0),
            rewardPoolId
        );
    }

    /// @notice Mark content as dormant if it hasn't reached milestone 0 within DORMANCY_PERIOD.
    /// @dev Anyone can call this. The mandatory submission bounty is not refunded.
    function markDormant(uint256 contentId) external nonReentrant whenNotPaused {
        Content storage c = contents[contentId];
        require(c.id != 0, "Content does not exist");
        require(c.status == ContentStatus.Active, "Not active");
        require(block.timestamp > dormancyAnchorAt[contentId] + DORMANCY_PERIOD, "Dormancy period not elapsed");
        require(!_hasOpenRound(contentId), "Content has active round");

        c.status = ContentStatus.Dormant;

        bytes32 submissionKey = contentSubmissionKey[contentId];
        if (submissionKey != bytes32(0)) {
            dormantKeyReleasableAt[contentId] = block.timestamp + DORMANT_EXCLUSIVE_REVIVAL_PERIOD;
        }

        emit ContentDormant(contentId);
    }

    /// @notice Revive dormant content by staking REVIVAL_STAKE LREP tokens.
    /// @dev Resets the activity timer. Max MAX_REVIVALS revivals per content.
    ///      Revival stake is sent to treasury (non-refundable).
    function reviveContent(uint256 contentId) external nonReentrant whenNotPaused {
        Content storage c = contents[contentId];
        require(c.status == ContentStatus.Dormant, "Not dormant");
        require(c.dormantCount < MAX_REVIVALS, "Max revivals reached");

        bytes32 submissionKey = contentSubmissionKey[contentId];
        require(submissionKey != bytes32(0), "Dormant key released");
        require(submissionKeyUsed[submissionKey], "Dormant key released");
        require(_isSubmitterIdentity(contentId, msg.sender), "Not original submitter");
        require(block.timestamp <= dormantKeyReleasableAt[contentId], "Revival window elapsed");

        // M-1/M-2 fix: send revival stake to treasury instead of leaving it unaccounted
        require(treasury != address(0), "Treasury not set");
        lrepToken.safeTransferFrom(msg.sender, treasury, REVIVAL_STAKE);

        c.status = ContentStatus.Active;
        c.dormantCount++;
        c.lastActivityAt = uint48(block.timestamp);
        dormancyAnchorAt[contentId] = block.timestamp;
        delete dormantKeyReleasableAt[contentId];
        c.reviver = msg.sender;

        emit ContentRevived(contentId, msg.sender);
    }

    /// @notice Release a dormant content key after the exclusive revival window expires.
    function releaseDormantSubmissionKey(uint256 contentId) external nonReentrant whenNotPaused {
        Content storage c = contents[contentId];
        require(c.id != 0, "Content does not exist");
        require(c.status == ContentStatus.Dormant, "Not dormant");

        bytes32 submissionKey = contentSubmissionKey[contentId];
        require(submissionKey != bytes32(0), "No submission key");
        require(block.timestamp > dormantKeyReleasableAt[contentId], "Revival window active");

        submissionKeyUsed[submissionKey] = false;
        delete contentSubmissionKey[contentId];
        delete dormantKeyReleasableAt[contentId];

        emit DormantSubmissionKeyReleased(contentId, submissionKey);
    }

    // --- VotingEngine callbacks ---

    /// @dev Authorize a callback from the canonical voting engine. Rejects any caller that is
    ///      not `votingEngine` outright — a rotated (deauthorized-by-rotation) engine cannot
    ///      mutate state even on content the new engine has not yet touched. Returns the
    ///      canonical engine's generation so callers can stamp it onto per-content state.
    function _authorizeEngineCallback(uint256) private view returns (uint256 callerGeneration) {
        if (msg.sender != votingEngine) revert OnlyVotingEngine();
        callerGeneration = votingEngineCallbackGeneration[msg.sender];
        // Defense-in-depth: if revokeVotingEngine were ever (incorrectly) used on the canonical
        // engine, callerGeneration would be zero — fail closed.
        if (callerGeneration == 0) revert OnlyVotingEngine();
    }

    /// @notice Called by VotingEngine to update raw activity timestamp after commits.
    /// @dev Vote commits refresh UI-facing activity without extending the dormancy window.
    ///      Stamps the per-content generation so any pre-existing older-engine grants are
    ///      shut out from this content even before `revokeVotingEngine` is called.
    function updateActivity(uint256 contentId) external {
        uint256 callerGeneration = _authorizeEngineCallback(contentId);

        address trackedEngine = contentRoundTrackingEngine[contentId];
        if (trackedEngine != address(0) && trackedEngine != msg.sender && _engineHasOpenRound(trackedEngine, contentId))
        {
            revert ActiveRoundOnPreviousEngine();
        }
        contentRoundTrackingEngine[contentId] = msg.sender;
        contents[contentId].lastActivityAt = uint48(block.timestamp);
        if (callerGeneration > contentSettlementEngineGeneration[contentId]) {
            contentSettlementEngineGeneration[contentId] = callerGeneration;
        }
    }

    /// @notice Called by VotingEngine when content reaches milestone 0 through a settled round.
    function recordMeaningfulActivity(uint256 contentId) external {
        uint256 callerGeneration = _authorizeEngineCallback(contentId);
        contents[contentId].lastActivityAt = uint48(block.timestamp);
        dormancyAnchorAt[contentId] = block.timestamp;
        if (callerGeneration > contentSettlementEngineGeneration[contentId]) {
            contentSettlementEngineGeneration[contentId] = callerGeneration;
        }
    }

    function updateRatingState(
        uint256 contentId,
        uint256 roundId,
        uint16 referenceRatingBps,
        RatingLib.RatingState calldata nextState
    ) external {
        uint256 callerGeneration = _authorizeEngineCallback(contentId);

        Content storage c = contents[contentId];
        require(c.id != 0, "Content does not exist");
        contentSettlementEngineGeneration[contentId] = callerGeneration;

        RatingLib.RatingState storage state = _ratingState[contentId];
        uint16 oldRatingBps = state.ratingBps == 0 ? uint16(uint256(c.rating) * 100) : state.ratingBps;
        uint8 oldDisplayRating = c.rating;
        uint16 clampedRatingBps = RatingMath.clampRatingBps(nextState.ratingBps);
        uint16 clampedConservativeRatingBps =
            nextState.conservativeRatingBps > clampedRatingBps ? clampedRatingBps : nextState.conservativeRatingBps;

        state.ratingLogitX18 = nextState.ratingLogitX18;
        state.confidenceMass = nextState.confidenceMass;
        state.effectiveEvidence = nextState.effectiveEvidence;
        state.settledRounds = nextState.settledRounds;
        state.ratingBps = clampedRatingBps;
        state.conservativeRatingBps = clampedConservativeRatingBps;
        state.lastUpdatedAt = nextState.lastUpdatedAt == 0 ? uint48(block.timestamp) : nextState.lastUpdatedAt;
        state.lowSince = nextState.lowSince;

        uint8 newDisplayRating = RatingMath.displayRatingFromBps(clampedRatingBps);
        if (newDisplayRating != oldDisplayRating) {
            c.rating = newDisplayRating;
            emit RatingUpdated(contentId, oldDisplayRating, newDisplayRating);
        }

        emit RatingStateUpdated(
            contentId,
            roundId,
            referenceRatingBps,
            oldRatingBps,
            clampedRatingBps,
            clampedConservativeRatingBps,
            nextState.confidenceMass,
            nextState.effectiveEvidence,
            nextState.settledRounds
        );
    }

    // --- View functions ---

    function getRatingState(uint256 contentId) external view returns (RatingLib.RatingState memory state) {
        state = _ratingState[contentId];
    }

    function getRating(uint256 contentId) external view returns (uint16) {
        uint16 ratingBps = _ratingState[contentId].ratingBps;
        if (ratingBps == 0) return uint16(uint256(contents[contentId].rating) * 100);
        return ratingBps;
    }

    function isContentActive(uint256 contentId) external view returns (bool) {
        Content storage c = contents[contentId];
        return c.id != 0 && c.status == ContentStatus.Active;
    }

    function isDormancyEligible(uint256 contentId) external view returns (bool) {
        Content storage c = contents[contentId];
        if (c.id == 0 || c.status != ContentStatus.Active) return false;
        if (block.timestamp <= dormancyAnchorAt[contentId] + DORMANCY_PERIOD) return false;
        return !_hasOpenRound(contentId);
    }

    /// @notice Preview the resolved category and question-level submission key for a future multi-media reveal.
    function previewQuestionSubmissionKey(
        string calldata contextUrl,
        string[] calldata imageUrls,
        string calldata videoUrl,
        string calldata title,
        string calldata description,
        string calldata tags,
        uint256 categoryId
    ) external view returns (uint256 resolvedCategoryId, bytes32 submissionKey) {
        SubmissionMetadata memory metadata = _validatedContextSubmissionMetadata(
            contextUrl, imageUrls, videoUrl, title, description, tags, categoryId
        );
        resolvedCategoryId = _resolveQuestionSubmissionCategory(metadata);
        submissionKey = _deriveQuestionMediaSubmissionKey(metadata, resolvedCategoryId);
    }

    function _computeRevealCommitment(
        bytes32 submissionKey,
        bytes32 mediaHash,
        string memory title,
        string memory description,
        string memory tags,
        uint256 categoryId,
        bytes32 salt,
        address submitter,
        SubmissionRewardTerms memory rewardTerms,
        RoundLib.RoundConfig memory roundConfig,
        QuestionSpecCommitment memory spec
    ) internal pure returns (bytes32) {
        return keccak256(
            abi.encode(
                "curyo-question-reveal-v3",
                submissionKey,
                mediaHash,
                keccak256(abi.encode(title, description, tags)),
                categoryId,
                salt,
                submitter,
                _hashRewardTerms(rewardTerms),
                keccak256(
                    abi.encode(
                        roundConfig.epochDuration, roundConfig.maxDuration, roundConfig.minVoters, roundConfig.maxVoters
                    )
                ),
                spec.questionMetadataHash,
                spec.resultSpecHash
            )
        );
    }

    function _computeQuestionBundleHash(
        SubmissionMetadata[] memory metadataList,
        bytes32[] memory mediaHashes,
        uint256[] memory resolvedCategoryIds,
        BundleQuestionInput[] calldata questions
    ) internal pure returns (bytes32) {
        bytes32[] memory questionHashes = new bytes32[](metadataList.length);
        for (uint256 i = 0; i < metadataList.length; i++) {
            questionHashes[i] = keccak256(
                abi.encode(
                    "curyo-question-bundle-item-v2",
                    metadataList[i].url,
                    mediaHashes[i],
                    metadataList[i].title,
                    metadataList[i].description,
                    metadataList[i].tags,
                    resolvedCategoryIds[i],
                    questions[i].salt,
                    i,
                    questions[i].spec.questionMetadataHash,
                    questions[i].spec.resultSpecHash
                )
            );
        }
        return keccak256(abi.encode("curyo-question-bundle-v2", questionHashes));
    }

    function _computeBundleRevealCommitment(
        bytes32 bundleHash,
        address submitter,
        SubmissionRewardTerms memory rewardTerms,
        RoundLib.RoundConfig memory roundConfig
    ) internal pure returns (bytes32) {
        return keccak256(
            abi.encode(
                "curyo-question-bundle-reveal-v3",
                bundleHash,
                submitter,
                rewardTerms.asset,
                rewardTerms.amount,
                rewardTerms.requiredVoters,
                rewardTerms.requiredSettledRounds,
                rewardTerms.bountyClosesAt,
                rewardTerms.feedbackClosesAt,
                rewardTerms.bountyEligibility,
                roundConfig.epochDuration,
                roundConfig.maxDuration,
                roundConfig.minVoters,
                roundConfig.maxVoters
            )
        );
    }

    function _validateSubmissionReward(SubmissionRewardTerms memory rewardTerms) internal view {
        require(
            rewardTerms.asset == SUBMISSION_REWARD_ASSET_LREP || rewardTerms.asset == SUBMISSION_REWARD_ASSET_USDC,
            "Invalid reward asset"
        );
        require(rewardTerms.amount >= _minimumSubmissionReward(rewardTerms.asset), "Reward below minimum");
        require(rewardTerms.requiredVoters >= MIN_SUBMISSION_REWARD_REQUIRED_VOTERS, "Too few voters");
        require(rewardTerms.requiredSettledRounds >= MIN_SUBMISSION_REWARD_SETTLED_ROUNDS, "Too few rounds");
        require(rewardTerms.requiredSettledRounds <= MAX_SUBMISSION_REWARD_SETTLED_ROUNDS, "Too many rounds");
        require(
            rewardTerms.amount >= rewardTerms.requiredSettledRounds * rewardTerms.requiredVoters, "Reward too small"
        );
        require(rewardTerms.bountyClosesAt == 0 || rewardTerms.bountyClosesAt > block.timestamp, "Bad close");
        require(
            rewardTerms.feedbackClosesAt == 0 || rewardTerms.feedbackClosesAt > block.timestamp, "Bad feedback close"
        );
        require(
            rewardTerms.bountyClosesAt == 0 || rewardTerms.feedbackClosesAt == 0
                || rewardTerms.feedbackClosesAt <= rewardTerms.bountyClosesAt,
            "Feedback after bounty"
        );
        require(rewardTerms.bountyEligibility <= 1, "Invalid eligibility");
    }

    function _hashRewardTerms(SubmissionRewardTerms memory rewardTerms) internal pure returns (bytes32) {
        return keccak256(
            abi.encode(
                rewardTerms.asset,
                rewardTerms.amount,
                rewardTerms.requiredVoters,
                rewardTerms.requiredSettledRounds,
                rewardTerms.bountyClosesAt,
                rewardTerms.feedbackClosesAt,
                rewardTerms.bountyEligibility
            )
        );
    }

    function _minimumSubmissionReward(uint8 rewardAsset) internal view returns (uint256 minimum) {
        if (address(protocolConfig) != address(0)) {
            minimum = rewardAsset == SUBMISSION_REWARD_ASSET_LREP
                ? protocolConfig.minSubmissionLrepPool()
                : protocolConfig.minSubmissionUsdcPool();
        }
        return minimum == 0 ? DEFAULT_MIN_SUBMISSION_REWARD_POOL : minimum;
    }

    function _defaultRoundConfig() internal view returns (RoundLib.RoundConfig memory cfg) {
        if (address(protocolConfig) != address(0)) {
            (cfg.epochDuration, cfg.maxDuration, cfg.minVoters, cfg.maxVoters) = protocolConfig.config();
            return cfg;
        }
        cfg = RoundLib.RoundConfig({
            epochDuration: uint32(20 minutes),
            maxDuration: uint32(20 minutes),
            minVoters: uint16(3),
            maxVoters: uint16(200)
        });
    }

    function _validatedRoundConfig(RoundLib.RoundConfig memory roundConfig)
        internal
        view
        returns (RoundLib.RoundConfig memory cfg)
    {
        require(address(protocolConfig) != address(0), "Config not set");
        cfg = protocolConfig.validateRoundConfig(
            roundConfig.epochDuration, roundConfig.maxDuration, roundConfig.minVoters, roundConfig.maxVoters
        );
    }

    function _resolveSubmitterIdentity(address submitter) internal view returns (address) {
        if (submitter == address(0)) return address(0);
        IRaterIdentityRegistry identityRegistry = _raterRegistry();
        if (address(identityRegistry) == address(0)) return submitter;
        IRaterIdentityRegistry.ResolvedRater memory resolved = identityRegistry.resolveRater(submitter);
        return resolved.holder == address(0) ? submitter : resolved.holder;
    }

    function _isSubmitterIdentity(uint256 contentId, address account) private view returns (bool) {
        bytes32 submitterIdentityKey = contentSubmitterIdentityKey[contentId];
        if (submitterIdentityKey == bytes32(0)) {
            address submitterIdentity = getSubmitterIdentity[contentId];
            return submitterIdentity != address(0) && submitterIdentity == account;
        }
        IRaterIdentityRegistry.ResolvedRater memory resolved = _resolveRater(account);
        return resolved.identityKey == submitterIdentityKey;
    }

    function _snapshotSubmitterIdentity(address submitter)
        internal
        view
        returns (address submitterIdentity, bytes32 submitterIdentityKey)
    {
        if (submitter == address(0)) return (address(0), bytes32(0));
        IRaterIdentityRegistry.ResolvedRater memory resolved = _resolveRater(submitter);
        submitterIdentity = resolved.holder == address(0) ? submitter : resolved.holder;
        submitterIdentityKey =
            resolved.identityKey == bytes32(0) ? _addressIdentityKey(submitter) : resolved.identityKey;
    }

    function _resolveRater(address account)
        internal
        view
        returns (IRaterIdentityRegistry.ResolvedRater memory resolved)
    {
        IRaterIdentityRegistry identityRegistry = _raterRegistry();
        if (address(identityRegistry) != address(0)) {
            resolved = identityRegistry.resolveRater(account);
            if (resolved.identityKey != bytes32(0)) return resolved;
        }

        resolved = IRaterIdentityRegistry.ResolvedRater({
            holder: account,
            identityKey: _addressIdentityKey(account),
            humanNullifier: bytes32(0),
            hasActiveHumanCredential: false,
            delegated: false
        });
    }

    function _raterRegistry() internal view returns (IRaterIdentityRegistry identityRegistry) {
        if (address(protocolConfig) == address(0)) return IRaterIdentityRegistry(address(0));
        return IRaterIdentityRegistry(protocolConfig.raterRegistry());
    }

    function _addressIdentityKey(address account) internal pure returns (bytes32) {
        if (account == address(0)) return bytes32(0);
        return keccak256(abi.encodePacked("rateloop.address-identity-v1", account));
    }

    function _hasOpenRound(uint256 contentId) internal view returns (bool) {
        address trackedEngine = contentRoundTrackingEngine[contentId];
        if (trackedEngine != address(0) && _engineHasOpenRound(trackedEngine, contentId)) return true;
        address currentEngine = votingEngine;
        return currentEngine != trackedEngine && _engineHasOpenRound(currentEngine, contentId);
    }

    function _engineHasOpenRound(address engine, uint256 contentId) internal view returns (bool) {
        uint256 activeRoundId = IRoundVotingEngine(engine).currentRoundId(contentId);
        if (activeRoundId == 0) return false;

        (, RoundLib.RoundState roundState,,,,,,,,,,,,) = IRoundVotingEngine(engine).rounds(contentId, activeRoundId);
        return roundState == RoundLib.RoundState.Open;
    }

    function _getInitialConfidenceMass() internal view returns (uint256) {
        if (address(protocolConfig) == address(0)) {
            return DEFAULT_CONFIDENCE_MASS_INITIAL;
        }

        uint256 initialConfidenceMass = protocolConfig.getInitialConfidenceMass();
        return initialConfidenceMass == 0 ? DEFAULT_CONFIDENCE_MASS_INITIAL : initialConfidenceMass;
    }

    function _getCurrentSlashConfig() internal view returns (RatingLib.SlashConfig memory slashConfig) {
        if (address(protocolConfig) == address(0)) {
            slashConfig = RatingLib.SlashConfig({
                slashThresholdBps: DEFAULT_SLASH_THRESHOLD_BPS,
                minSlashSettledRounds: DEFAULT_MIN_SLASH_SETTLED_ROUNDS,
                minSlashLowDuration: DEFAULT_MIN_SLASH_LOW_DURATION,
                minSlashEvidence: DEFAULT_MIN_SLASH_EVIDENCE
            });
            return slashConfig;
        }

        (
            slashConfig.slashThresholdBps,
            slashConfig.minSlashSettledRounds,
            slashConfig.minSlashLowDuration,
            slashConfig.minSlashEvidence
        ) = protocolConfig.slashConfig();
    }

    function getSlashConfigForContent(uint256 contentId)
        public
        view
        returns (RatingLib.SlashConfig memory slashConfig)
    {
        slashConfig = contentSlashConfigSnapshot[contentId];
        if (slashConfig.slashThresholdBps == 0) {
            return _getCurrentSlashConfig();
        }
    }

    // --- Admin ---

    function pause() external onlyRole(PAUSER_ROLE) {
        _pause();
    }

    function unpause() external onlyRole(PAUSER_ROLE) {
        _unpause();
    }
}
